group "jp.nephy"
version "3.0.22"

buildscript {
    ext.kotlin_version = "1.2.70"
    ext.ktor_version = "0.9.4"

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

apply plugin: "kotlin"
apply plugin: "org.jetbrains.dokka"
apply plugin: "maven"
apply plugin: "signing"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "io.ktor:ktor-client-apache:$ktor_version"
    testCompile "io.ktor:ktor-client-cio:$ktor_version"
    testCompile "io.ktor:ktor-client-jetty:$ktor_version"
    testCompile "io.ktor:ktor-client-okhttp:$ktor_version"

    compile "jp.nephy:jsonkt:3.5"
    compile "com.twitter.twittertext:twitter-text:3.0.1"

    compile "io.github.microutils:kotlin-logging:1.5.9"
    testCompile "ch.qos.logback:logback-core:1.2.3"
    testCompile "ch.qos.logback:logback-classic:1.2.3"
    testCompile "org.fusesource.jansi:jansi:1.17.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { hasProperty("deploy") }
    sign configurations.archives
}

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
}

if (hasProperty("deploy")) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                def env = System.getenv()

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication userName: env["sonatypeUsername"], password: env["sonatypePassword"]
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication userName: env["sonatypeUsername"], password: env["sonatypePassword"]
                }

                pom.project {
                    name "Penicillin"
                    packaging "jar"
                    artifactId "penicillin"
                    description "Penicillin is a simple Twitter API wrapper."
                    url "https://github.com/NephyProject/Penicillin"

                    licenses {
                        license {
                            name "MIT"
                            url "http://opensource.org/licenses/MIT"
                        }
                    }

                    developers {
                        developer {
                            id "SlashNephy"
                            name "Slash Nephy"
                            email "admin@nephy.jp"
                        }
                    }

                    scm {
                        url "https://github.com/NephyProject/Penicillin"
                        connection "scm:git:https://github.com/NephyProject/Penicillin.git"
                        developerConnection "scm:git:git@github.com:NephyProject/Penicillin.git"
                        tag "HEAD"
                    }

                    issueManagement {
                        system "GitHub Issues"
                        url "https://github.com/NephyProject/Penicillin/issues"
                    }
                }
            }
        }
    }
}
